Exercises
#########


Multiple Choice
===============

EXERCISE My Multiple Choice Exercise  @ex:myMultiChoice
    Choose the right answers:
    [x] This answer is correct.
    [ ] This answer is incorrect.
    [x] This answer is correct.

EXERCISE My Multiple Choice Exercise with static order  @ex:myMultiChoice
    ORDER=static
    Choose the right answers:
    [x] This answer is correct.
    [ ] This answer is incorrect.
    [x] This answer is correct.


EXERCISE My dynamic Multiple Choice Exercise  @ex:myMultiChoice
    CODE
        let x/y/z/w = rand(10, 20);    % no pair of x, y, z, w is equal
        let c1 = x > w
        let c2 = y > w
        let c3 = z > w
    Choose the right answers:
    [:c1] $x > w$
    [:c2] $y > w$
    [:c3] $z > w$
    [x]   $1 > 0$    % statically true
    [ ]   $1 < 0$    % statically false


Single Choice
=============

EXERCISE My Single Choice Exercise  @ex:mySingleChoice
    Choose the right answer:
    (x) This answer is correct.
    ( ) This answer is incorrect.
    ( ) This answer is incorrect.


Randomized Exercises
====================

EXERCISE Addition  @ex:add
    CODE
        let x/y = rand(1, 5)
        let z = x + y
    Calculate $ x + y = $ #z.

EXERCISE Matrix Multiplication @ex:add-mul
    CODE
        let x:y = rand<2,2>(1, 5)
        let z = x * y
    Calculate $ x \cdot y = $ #z.
        
EXERCISE Differentiation
    CODE
        let f(u,v) = u^2 + v^2
        let fu(u,v) = diff(f,u)
        let fv(u,v) = diff(f,v)
    Let $"f"(u,v)=f$. 
    Calculate $\frac{\partial"f"}{\partial u}(u,v)=$ #fu

EXERCISE Integration
    CODE
        let u1 = rand(2,9)
        let f(x) = u1
    Calculate $ \int f ~ dx = $ #f,DIFF=x $ + C$
    
    $(C \in \RR)$

Mixed Exercises
===============

EXERCISE Mixed
    CODE
        let x/y = rand(1, 5)
        let z = x + y
    Calculate $ x + y = $ #z.

    Choose the right answers:
    [x] This answer is correct.
    [ ] This answer is incorrect.
    [x] This answer is correct.
    Choose the right answers:
    [x] True.
    [x] True.
    [ ] False.


Exercise with button answers
============================


EXERCISE Multiplication
    CHOICES=4
    CODE
        let x/y = rand(10, 20)
        let z = x * y
    $x \cdot y=$ #z


EXERCISE Simple Integration  @ex:intSimple
    CODE
        let F(x) = (1/3) * x^3 + 7*x
    Solve $\int (x^2+7) dx = $  #F,build_term  $+C$


Scoring
=======

EXERCISE Scoring example
    SCORES=5        % total score of the exercise (5/3 for fa; 10/3 for fb)
    CODE
        let a = rand(2, 4)
        let b = rand(5, 6)
        let fa = fac(a)
        let fb = fac(b)
    Calculate
    -) $ a! = $ #fa            % if scores are not given for a solution,
    -) $ b! = $ #fb,SCORE=2    % it defaults to 1, i.e. fa has score 1


Gap Exercise
============

EXERCISE My Gap Exercise 1  @ex:myLabel
    This is a gap exercise with limited keys and hints on the number of letters.

    Garfield is a #"cat".

EXERCISE My Gap Exercise 2  @ex:myLabel
    This is gap exercise with all letters and no length hint.

    Rain is #"wet",HIDE_LENGTH,SHOW_ALL_LETTERS.


Exercise with forced keyboard
=============================

EXERCISE Keyboard exapmle
    CODE
        let x = 5
    This example shows how to change the keyboard for the input field to `integerSet`.
    The answer is five.
    
    #x,KEYBOARD=integerSet


Arrangement Exercise
====================

EXERCISE Arrangement exercise
    CODE
        let n = rand(5,10)        % length
        let f = zeros<n>()        % row vector for the sequence
        f[0] = 0
        f[1] = 1
        for k from 2 to n-1 {     % calc sequence iteratively
            f[k] = f[k-2] + f[k-1]
        }
    Arrange the following numbers to get the first numbers of the Fibonacci sequence (solution is $f$): 
    
    #f,ARRANGE


Timed Exercise
==============

EXERCISE Title @ex:label
    TIMER=3
    ACCELERATE=true
    STOP_AFTER_ERRORS=1
    CHOICES=4
    CODE
        let x:y = rand(20,50)
        let z = x + y
    Calculate $x+y=$ #z
