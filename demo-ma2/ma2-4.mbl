Funktionen von mehreren Variablen
#################################

EXERCISE Komponentenfunktionen
    CODE
        a:b:c:d:e:f:g:h:i = rand(0,1)
        f1(x,y,z) = a*x + b*y + c*z
        f2(x,y,z) = d*x + e*y + f*z
        f3(x,y,z) = g*x + h*y + i*z
    Sei $ f : \RR^3 \to \RR^3 $; $ f(x,y,z) = (f_1,f_2,f_3) $ ein Vektorfeld.
    Bestimme die **Komponentenfunktionen** von $f$:
    - $f_1(x,y,z)=$ #f1
    - $f_2(x,y,z)=$ #f2
    - $f_3(x,y,z)=$ #f3

EXERCISE Partielle Ableitungen
    CODE
        a:b:c:d:e = rand(2,5)
        f(x,y) = a*x^b + c*x^d*y^e
        fx(x,y) = diff(f, x)
        fy(x,y) = diff(f, y)
    Sei $"f"(x,y)=f$. Bestimme die **partiellen Ableitungen**:
    - $\frac{\partial f}{\partial x}=$ #fx
    - $\frac{\partial f}{\partial y}=$ #fy

EXERCISE Partielle Ableitungen
    CODE
        a = rand(0,1)
        b = 1 - a
        c = rand(4,8)
        f(x,y) = c / (a*x^2 + b*y^2)
        fx(x,y) = diff(f, x)
        fy(x,y) = diff(f, y)
    Sei $"f"(x,y)=f$. Bestimme die **partiellen Ableitungen**:
    - $\frac{\partial f}{\partial x}=$ #fx
    - $\frac{\partial f}{\partial y}=$ #fy

EXERCISE Gradient
    CODE
        a:b = rand(0,1)
        c = rand(2,5)
        f(x,y) = - x^3 - a*y^3 + b*c*x*y
        fx(x,y) = diff(f, x)
        fy(x,y) = diff(f, y)
    Gegeben sei eine Funktion $"f"(x,y)=f$.
    Bestimme den **Gradienten**:
    - $\nabla"f($  #fx  ,  #fy  $)$

EXERCISE Gradient
    CODE
        c = rand(2,5)
        f(u,v) = u^2 / v^2 + c
        fu(u,v) = diff(f, u)
        fv(u,v) = diff(f, v)
    Gegeben sei eine Funktion $"f"(u,v)=f$.
    Bestimme den **Gradienten**:
    - $\nabla"f($  #fu  ,  #fv  $)$

EXERCISE Gradient
    CODE
        a = rand(0,1)
        b = 1 - a
        c:d = rand(2,5)
        f(x,y) = exp(c*x) * (a*sin(d*y) + b*cos(d*y))
        fx(x,y) = diff(f, x)
        fy(x,y) = diff(f, y)
    Gegeben sei eine Funktion $"f"(x,y)=f$.
    Bestimme den **Gradienten**:
    - $\nabla"f($  #fx  ,  #fy  $)$
    *Hinweis: gebe die Exponentialfunktion in der Form `exp(x)` an.*

% %%%    TODO
%
%
%Jacobi-Matrix
%
%	a in { 0, 1 }
%	b = 1 - a
%	c in { -1, 1 }
%	d in { 1, 2, ..., 5}
%	f1(x,y) = a * x * y * sin(x) + b * x * y * cos(x)
%	f2(x,y) = x^2 + c * y^2 + d
%	J(x,y) = [ [ diff(f1,x), diff(f1,y) ], [ diff(f2,x), diff(f2,y) ] ]
%
%Sei $f(x,y) = ( f1, f2 )$.
%Bestimmen Sie die __Jacobi-Matrix__:
%* $ "J_f" = #J $


EXERCISE Rotation und Divergenz
    CODE
        a:b = rand(0,1)
        c = 1 - a
        dd = 1 - b
        e = rand(2,9)
        f1(x,y,z) = a * x + c * z
        f2(x,y,z) = b * sin(PI*y) + dd * cos(PI*x)
        f3(x,y,z) = x^3 + e * z^2
        rx(x,y,z) = diff(f3,y) - diff(f2,z)
        ry(x,y,z) = diff(f1,z) - diff(f3,x)
        rz(x,y,z) = diff(f2,x) - diff(f1,y)
        d(x,y,z) = diff(f1,x) + diff(f2,y) + diff(f3,z)
    Sei $f(x,y) = ( f1, f2, f3 )$.
    Bestimme **Rotation** und **Divergenz** des Vektorfeldes.
    - rot$(f)=($  #rx  ,  #ry ,  #rz  $)$
    - div$(f)=$ #d

EXERCISE Höhere partielle Ableitungen
    CODE
        a = rand(0,1)
        b = 1 - a
        c:d = rand(3,4)
        f(x,y) = c * x^d * y + x^(d-1)
        dxx(x,y) = diff( diff(f,x), x)
        dxy(x,y) = diff( diff(f,x), y)
        dyy(x,y) = diff( diff(f,y), y)
    Sei $"f"(x,y)=f$. Bestimme die folgenden **höheren partiellen Ableitungen**:
    - $\frac{\partial^2 "f"}{\partial x^2}=$ #dxx
    - $\frac{\partial^2 "f"}{\partial x \partial y}=$ #dxy
    - $\frac{\partial^2 "f"}{\partial y^2}=$ #dyy

EXERCISE Stationäre Stellen
    CODE
        a:b = rand(2,8)
        f(x,y) = a*x^2 - b*y^2
        fx(x,y) = diff(f, x)
        fy(x,y) = diff(f, y)
        sx = 0
        sy = 0
    Gegeben sei eine Funktion $"f"(x,y)=f$.
    - Bestimme zunächst den **Gradienten**:
    $\nabla "f"=($  #fx  ,  #fy $)$
    - Gebe die (einzige) **stationäre Stelle** an:
    $($  #sx  ,  #sy $)$

EXERCISE Definitheit
    CODE
        a:b:c = randZ(-1,2)
        A = [[a,0,0],[0,b,0],[0,0,c]]
        s = {a,b,c}
        p1 = min(s) > 0
        p2 = max(s) < 0
        p3 = !(p1 || p2)
    Sei $"A"=A$ eine Matrix über $\RR$.
    Welche der folgenden Aussagen ist korrekt?
    [$p1$] $"A"$ ist **positiv definit**
    [$p2$] $"A"$ ist **negativ definit**
    [$p3$] $"A"$ ist **indefinit**

EXERCISE Definitheit
    CODE
        A = rand<2,2>(1,4)
        while (is_invertible(A) == false || is_symmetric(A) == false) {
            A = rand<2,2>(1,4)
        }
        s = eigenvalues_sym(A)
        p1 = min(s) > 0
        p2 = max(s) < 0
        p3 = !(p1 || p2)
    Sei $"A"=A$ eine Matrix über $\RR$.
    Welche der folgenden Aussagen ist korrekt?
    [$p1$] $"A"$ ist **positiv definit**
    [$p2$] $"A"$ ist **negativ definit**
    [$p3$] $"A"$ ist **indefinit**

EXERCISE Hesse-Matrix
    CODE
        a = rand(2,8)
        b = rand(2,4)
        f(x,y) = a * x^b * y^(b+1)
        h11(x,y) = diff( diff(f,x), x)
        h12(x,y) = diff( diff(f,x), y)
        h21(x,y) = diff( diff(f,y), x)
        h22(x,y) = diff( diff(f,y), y)
        % TODO: let H = [ [ h11, h12 ], [ h21, h22 ] ]
    Sei $"f"(x,y) = f$.
    Bestimme die **Hesse-Matrix**:
    - $ "H" "f"=$ #H

EXERCISE Hesse-Matrix
    CODE
        a:b = rand(0,1)
        c = 1 - a
        d = 1 - b
        e = rand(2,8)
        f(x,y) = exp(a*x+c*y) + e * x^2 + y^2 + b*cos(y) + d*sin(y)
        h11(x,y) = diff( diff(f,x), x)
        h12(x,y) = diff( diff(f,x), y)
        h21(x,y) = diff( diff(f,y), x)
        h22(x,y) = diff( diff(f,y), y)
        % TODO: let H = [ [ h11, h12 ], [ h21, h22 ] ]
    Sei $"f"(x,y) = f$.
    Bestimme die **Hesse-Matrix**:
    - $ "H" "f"=$ #H
    *Hinweis: gebe die Exponentialfunktion in der Form `exp(x)` an.*

EXERCISE Extremwerte
    CODE
        w1 = rand(0,1)
        w2 = 1 - w1
        w3:w4:w5:w6:w7:w8:w9:w10 = rand(2,3)
        f(x,y) = w4*x^w5*y^w6 + w7*x^w8 + w9*y^w10
        h11(x,y) = diff( diff(f,x), x)
        h12(x,y) = diff( diff(f,x), y)
        h21(x,y) = diff( diff(f,y), x)
        h22(x,y) = diff( diff(f,y), y)
        % TODO: let H = [ [ h11, h12 ], [ h21, h22 ] ]
        ax = 0
        ay = 0
        A = [ [ h11(ax,ay), h12(ax,ay) ], [ h21(ax,ay), h22(ax,ay) ] ]
        lambda = eigenvalues_sym(A)
        p1 = min(lambda) > 0
        p2 = max(lambda) < 0
        p3 = !(p1 || p2)
    Sei $"f"(x,y) = f$.
    Bestimme die **Hesse-Matrix**:
    - $"H" "f"=$ #H
    Betrachte die potentielle Extremstelle $ a=(ax,ay) $ und berechne die Eigenwerte von $ ("H" "f")(a) $:
    - $"lambda"=$ #lambda
    Welche der folgenden Aussagen ist korrekt?
    [$p1$] $"f"$ besitzt bei $a$ ein **lokales Minimum**
    [$p2$] $"f"$ besitzt bei $a$ ein **lokales Maximum**
    [$p3$] $"f"$ besitzt bei $a$ einen **Sattelpunkt**

EXERCISE Fehlerfortpflanzung
    CODE
        a:b:c:vx:vy = rand(2,4)
        f(x,y) = a*x^b * y^c + x*y
        ex(x,y) = diff(f, x)
        ey(x,y) = diff(f, y)
        delta = 0.1
        zmax = abs(ex(vx,vy))*delta + abs(ey(vx,vy))*delta
    Sei $"f"(x,y) = f$.
    Bestimmen die **maximale Messunsicherheit** bei **linearer Fehlerfortpflanzung**:
    - $ (\Delta z)_{\max} = \abs("a") \Delta "x" + \abs("b") \Delta "y" $; $"a"=$  #ex  ,  $"b"=$  #ey
    Seien nun $x=vx$, $y=vy$ und $\Delta x = \Delta y = \delta$.
    Berechnen die folgende Fehlergröße:
    - $ (\Delta z)_{\max} =$  #zmax
